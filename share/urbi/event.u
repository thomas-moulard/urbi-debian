/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

// Completion of events code

do (Event)
{
  function '||' (rhs)
  {
    var res = Event.new |
    at (this?)
      res! |
    at (rhs?)
      res! |
    return res
  };

  // This function does not handle events that have a duration because
  // this would be costly for the declarations of "at" with
  // conditions.
  function '<<' (rhs)
  {
    rhs.onEvent(closure (var evt) { true },
                closure (var evt, var pattern) { this.trigger },
                closure (var evt, var pattern) { this.stop }) |

    // To watch many at once.
    this
  };

  // Event that triggers if the original event is up for the given
  // duration. That is, "this ~ duration".
  function persists(var duration)
  {
    var res = Event.new |
    var control = Tag.new |
    at (this?)
    {
      control:
        {
          sleep(duration)|
          var '$pattern'.instance = res.getSlot("trigger").apply([res] + '$evt'.payload)|
        }
    }
    onleave
    {
      control.stop |
      if ('$pattern'.hasLocalSlot("instance"))
      {
        '$pattern'.instance.stop |
        '$pattern'.removeSlot("instance") |
      }
    };
    return res
  };
};
