%% Copyright (C) 2009-2010, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{InputStream}

InputStreams are used to read (possibly binary) files by hand.
\refObject{File} provides means to swallow a whole file either as a
single large string, or a list of lines.  \lstinline|InputStream|
provides a more fine-grained interface to read files.

\subsection{Prototypes}
\begin{refObjects}
\item[Object]
\end{refObjects}

\begin{windows}
  Beware that because of limitations in the current implementation,
  one cannot safely read from two different files at the same time
  under Windows.
\end{windows}

\subsection{Construction}

An InputStream is a reading-interface to a file, so its constructor
requires a \refObject{File}.

\begin{urbiscript}[firstnumber=1]
File.save("file.txt", "1\n2\n");
var is = InputStream.new(File.new("file.txt"));
[00000001] InputStream_0x827000
\end{urbiscript}

Bear in mind that open streams should be closed
(\autoref{sec:specs:OutputStream:ctor}).

\begin{urbiscript}
is.close;
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item[close] Close the stream, return void.  Raise an error if the file is
  closed.
\begin{urbiscript}
{
  var i = InputStream.new(File.create("file.txt"));
  assert(i.close.isVoid);
  i.close;
};
[00000001:error] !!! close: stream is closed
\end{urbiscript}

\item[get]%
  Get the next available byte as a \refObject{Float}, or \lstinline|void| if
  the end of file was reached.  Raise an error if the file is closed.
\begin{urbiscript}
{
  File.save("file.txt", "1\n2\n");
  var i = InputStream.new(File.new("file.txt"));
  var x;
  while (!(x = i.get.acceptVoid).isVoid)
    cout << x;
  i.close;
  i.get;
};
[00000001:output] 49
[00000002:output] 10
[00000003:output] 50
[00000004:output] 10
[00000005:error] !!! get: stream is closed
\end{urbiscript}

\item[getChar]%
  Get the next available byte as a \refObject{String}, or \lstinline|void|
  if the end of file was reached.  Raise an error if the file is closed.
\begin{urbiscript}
{
  File.save("file.txt", "1\n2\n");
  var i = InputStream.new(File.new("file.txt"));
  var x;
  while (!(x = i.getChar.acceptVoid).isVoid)
    cout << x;
  i.close;
  i.getChar;
};
[00000001:output] "1"
[00000002:output] "\n"
[00000003:output] "2"
[00000004:output] "\n"
[00000005:error] !!! getChar: stream is closed
\end{urbiscript}

\item[getLine]%
  Get the next available line as a \refObject{String}, or \lstinline|void|
  if the end of file was reached.  The end-of-line characters are trimmed.
  Raise an error if the file is closed.
\begin{urbiscript}
{
  File.save("file.txt", "1\n2\n");
  var i = InputStream.new(File.new("file.txt"));
  var x;
  while (!(x = i.getLine.acceptVoid).isVoid)
    cout << x;
  i.close;
  i.getLine;
};
[00000001:output] "1"
[00000002:output] "2"
[00000005:error] !!! getLine: stream is closed
\end{urbiscript}
\end{urbiscriptapi}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
